apply plugin: 'com.android.library'
apply plugin: 'jacoco-android'
apply plugin: 'com.github.kt3k.coveralls'

group = 'com.github.Wolox'

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 27
        consumerProguardFiles 'proguard-retrofit.pro', 'proguard-okhttp.pro', 'proguard-gson.pro',
                'proguard-joda-time.pro'
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

jacocoAndroidUnitTestReport {
    excludes = defaultExcludes + ['**/*_Factory.class']
}

coveralls {
    jacocoReportPath = "${buildDir}/reports/jacoco/jacocoTestReleaseUnitTestReport/jacocoTestReleaseUnitTestReport.xml"
}

buildscript {
    ext.wolmo_version = '2.0.3'
    ext.retrofit_version = '2.3.0'
    ext.okhttp3_version = '3.9.0'
    ext.dagger_version = '2.13'
    ext.joda_version = '2.10'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Wolmo
    implementation "com.github.Wolox:wolmo-core-android:$wolmo_version"

    // Dagger
    annotationProcessor "com.google.dagger:dagger-compiler:$dagger_version"

    // Third party
    api "com.squareup.retrofit2:retrofit:$retrofit_version"
    api "com.squareup.retrofit2:converter-gson:$retrofit_version"
    api "com.squareup.okhttp3:okhttp-urlconnection:$okhttp3_version"
    api "com.squareup.okhttp3:okhttp:$okhttp3_version"
    api "joda-time:joda-time:$joda_version"

    // Test
    testImplementation "junit:junit:4.12"
    testImplementation "org.mockito:mockito-inline:2.13.0" // Mockito inline adds support for mocking final classes and methods
    testImplementation "org.assertj:assertj-core:3.9.0"
    testImplementation "com.squareup.okhttp3:mockwebserver:$okhttp3_version"
}